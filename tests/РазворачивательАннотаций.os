#Использовать ".."
#Использовать "."
#Использовать asserts

&Тест
&Параметры(Ложь)
&Параметры(Истина)
Процедура РазвернутьАннотацииСвойств(ВладелецОбъект) Экспорт

	Если ВладелецОбъект Тогда
		ВладелецАннотаций = Новый ВладелецАннотаций();
	Иначе
		ВладелецАннотаций = Тип("ВладелецАннотаций");
	КонецЕсли;

	// Дано
	КонтейнерАннотаций = Новый КонтейнерАннотаций();
	Рефлектор = Новый Рефлектор();
	ТаблицаМетодов = Рефлектор.ПолучитьТаблицуМетодов(ВладелецАннотаций);
	Методы = РаботаСАннотациями.НайтиМетодыСАннотацией(ТаблицаМетодов, "АннотацияВерхнеуровневая");

	// Когда
	КонтейнерАннотаций.ДобавитьАннотацию(Тип("АннотацияТестоваяАннотация"));
	КонтейнерАннотаций.ДобавитьАннотацию(Тип("АннотацияВерхнеуровневая"));
	РазворачивательАннотаций = КонтейнерАннотаций.ПолучитьРазворачивательАннотаций();
	РазворачивательАннотаций.РазвернутьАннотацииСвойств(Методы, ВладелецАннотаций);

	Аннотации = Методы[0].Аннотации;

	// Тогда
	Ожидаем.Что(Аннотации.Количество()).Равно(3);
	Ожидаем.Что(Аннотации[2].Имя).Равно("тестоваяаннотация");
	Ожидаем.Что(Аннотации[2].Параметры[0].Значение).Равно(1);

КонецПроцедуры

&Тест
Процедура МетодСНесколькимиНеПовторяемымиЗначениями() Экспорт

	// Дано
	ВладелецАннотаций = Тип("ВладелецАннотаций");
	КонтейнерАннотаций = Новый КонтейнерАннотаций();
	Рефлектор = Новый Рефлектор();
	ТаблицаМетодов = Рефлектор.ПолучитьТаблицуМетодов(ВладелецАннотаций);
	Методы = РаботаСАннотациями.НайтиМетодыСАннотацией(ТаблицаМетодов, "ТестоваяАннотация");

	Параметры = Новый Массив();
	Параметры.Добавить(Методы);
	Параметры.Добавить(ВладелецАннотаций);

	// Когда
	КонтейнерАннотаций.ДобавитьАннотацию(Тип("АннотацияТестоваяАннотация"));
	РазворачивательАннотаций = КонтейнерАннотаций.ПолучитьРазворачивательАннотаций();

	// Тогда
	Ожидаем.Что(РазворачивательАннотаций)
		.Метод("РазвернутьАннотацииСвойств", Параметры)
		.ВыбрасываетИсключение(
			"Аннотация ТестоваяАннотация в классе ВладелецАннотаций содержит несколько значений параметра Значение, "
			+ "но параметр не помечен аннотацией ""Повторяемый"""
		);

КонецПроцедуры

&Тест
Процедура МетодСНесколькимиПовторяемымиЗначениями() Экспорт

	// Дано
	ВладелецАннотаций = Тип("ВладелецАннотаций");
	КонтейнерАннотаций = Новый КонтейнерАннотаций();
	Рефлектор = Новый Рефлектор();
	ТаблицаМетодов = Рефлектор.ПолучитьТаблицуМетодов(ВладелецАннотаций);
	Методы = РаботаСАннотациями.НайтиМетодыСАннотацией(ТаблицаМетодов, "ТестоваяАннотацияСПовторяемымЗначением");

	Параметры = Новый Массив();
	Параметры.Добавить(Методы);
	Параметры.Добавить(ВладелецАннотаций);

	// Когда
	КонтейнерАннотаций.ДобавитьАннотацию(Тип("АннотацияСПовторяемымЗначением"));
	РазворачивательАннотаций = КонтейнерАннотаций.ПолучитьРазворачивательАннотаций();
	РазворачивательАннотаций.РазвернутьАннотацииСвойств(Методы, ВладелецАннотаций);

	Аннотации = Методы[0].Аннотации;

	// Тогда
	Ожидаем.Что(Аннотации.Количество()).Равно(1);
	Ожидаем.Что(Аннотации[0].Параметры[0].Значение).Равно(1);
	Ожидаем.Что(Аннотации[0].Параметры[1].Значение).Равно(2);

КонецПроцедуры
