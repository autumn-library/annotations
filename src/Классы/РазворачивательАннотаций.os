#Использовать logos

// КонтейнерАннотаций - Контейнер данных об аннотациях.
Перем _КонтейнерАннотаций;
// Рефлектор - стандартный рефлектор OneScript.
Перем _Рефлектор;
// Лог - экземпляр лога
Перем _Лог;

// Развернуть все аннотации переданного свойства рекурсивно.
// Для разворачивания используются данные из определений аннотаций.
//
// Параметры:
//   Свойство - Структура - Описание свойства, метода или параметра метода:
//     * Имя - Строка - Имя свойства.
//     * Аннотации - ТаблицаЗначений - Таблица аннотаций свойства:
//       ** Имя - Строка - Имя аннотации.
//        ** Параметры - ТаблицаЗначений - Таблица параметров аннотации:
//           *** Имя      - Строка - Имя параметра аннотации.
//           *** Значение - Строка - Значение параметра аннотации.
//   ВладелецСвойства - Тип - Тип владельца свойства.
//                    - Сценарий - Экземпляр владельца свойства.
//
Процедура РазвернутьАннотацииСвойства(Свойство, ВладелецСвойства) Экспорт
	
	ОпределенияАннотаций = _КонтейнерАннотаций.ПолучитьОпределенияАннотаций();
	Аннотации = Свойство.Аннотации;
	НакопленныеАннотации = Свойство.Аннотации.СкопироватьКолонки();
	
	РазвернутьАннотации(ОпределенияАннотаций, Аннотации, НакопленныеАннотации, ВладелецСвойства, Свойство);
	
	Свойство.Аннотации = НакопленныеАннотации;
	
КонецПроцедуры

// Развернуть все аннотации переданного свойства рекурсивно.
// Для разворачивания используются данные из определений аннотаций.
//
// Параметры:
//   Свойства - Массив Из Структура - Описания свойств, методов или параметров метода:
//     * Имя - Строка - Имя свойства.
//     * Аннотации - ТаблицаЗначений - Таблица аннотаций свойства:
//       ** Имя - Строка - Имя аннотации.
//        ** Параметры - ТаблицаЗначений - Таблица параметров аннотации:
//           *** Имя      - Строка - Имя параметра аннотации.
//           *** Значение - Строка - Значение параметра аннотации.
//   ВладелецСвойства - Тип - Тип владельца свойства.
//                    - Сценарий - Экземпляр владельца свойства.
//
Процедура РазвернутьАннотацииСвойств(Свойства, ВладелецСвойства) Экспорт
	
	Для Каждого Свойство Из Свойства Цикл
		РазвернутьАннотацииСвойства(Свойство, ВладелецСвойства);
	КонецЦикла;
	
КонецПроцедуры

Процедура РазвернутьАннотации(
	ОпределенияАннотаций,
	Аннотации,
	НакопленныеАннотации,
	ВладелецСвойства,
	Свойство,
	УровеньВложенности = 1
)
	
	Для Каждого Аннотация Из Аннотации Цикл
		
		ЭтоКорневаяАннотация = НРег(Аннотация.Имя) = НРег("Аннотация");
		
		Если НЕ ЭтоКорневаяАннотация Тогда
			НоваяАннотация = НакопленныеАннотации.Добавить();
			НоваяАннотация.Имя = Аннотация.Имя;
			
			НоваяАннотация.Параметры = Аннотация.Параметры.Скопировать();
		КонецЕсли;
		
		ОпределениеАннотации = ОпределенияАннотаций.Получить(НРег(Аннотация.Имя));
		
		Если ОпределениеАннотации = Неопределено Тогда
			_Лог.Отладка(
				"Обнаружена незарегистрированная аннотация ""%1"" у владельца ""%2"" на свойстве ""%3""",
				Аннотация.Имя,
				ВладелецСвойства,
				Свойство.Имя
			);

			Продолжить;
		КонецЕсли;
		
		ПодчиненныеАннотации = ОпределениеАннотации.Аннотации();
		
		// Проверяем только аннотации на самом верхнем уровне вложенности,
		// т.к. остальные проверяются при регистрации аннотации.
		Если УровеньВложенности = 1 Тогда
			ОпределениеАннотации.Проверить(Аннотация, ВладелецСвойства);
		КонецЕсли;

		ОбъектАннотации = ОпределениеАннотации.СоздатьОбъектАннотации(Аннотация);
			
		Если _Рефлектор.МетодСуществует(ОбъектАннотации, "ПриРазворачиванииАннотации") Тогда
			ОбъектАннотации.ПриРазворачиванииАннотации(
				ОпределениеАннотации,
				ПодчиненныеАннотации,
				ВладелецСвойства,
				Свойство
			);
		КонецЕсли;
		
		Если НЕ ЭтоКорневаяАннотация Тогда
			РазвернутьАннотации(
				ОпределенияАннотаций,
				ПодчиненныеАннотации,
				НакопленныеАннотации,
				ВладелецСвойства,
				Свойство,
				УровеньВложенности + 1
			);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПриСозданииОбъекта(КонтейнерАннотаций)
	_КонтейнерАннотаций = КонтейнерАннотаций;
	_Рефлектор = Новый Рефлектор();
	_Лог = Логирование.ПолучитьЛог("oscript.lib.annotations.РазворачивательАннотаций");
КонецПроцедуры